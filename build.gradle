buildscript {
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  apply from: new File("./dependencies.gradle")

  dependencies {
    classpath dep.kotlinGradlePlugin
    classpath dep.junitGradlePlugin
    classpath dep.shadowJarPlugin
    classpath dep.mavenPublishGradlePlugin
    classpath dep.wireGradlePlugin
  }
}

tasks.register("testShardNonHibernate") {
  group = "Continuous integration"
  description = "Runs all tests that don't depend on misk-hibernate. " +
          "This target is intended for manually sharding tests to make CI faster."
}

tasks.register("testShardHibernate") {
  group = "Continuous integration"
  description = "Runs all tests that depend on misk-hibernate. " +
          "This target is intended for manually sharding tests to make CI faster."
}

subprojects { subProject ->
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'org.jetbrains.dokka'

  buildscript {
    repositories {
      mavenCentral()
      jcenter()
    }

    dependencies {
      classpath dep.shadowJarPlugin
    }
  }
  repositories {
    mavenCentral()
    jcenter()
  }
  compileKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
      allWarningsAsErrors = true
    }
  }
  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
      allWarningsAsErrors = true
    }
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
  }

  dependencies {
    testImplementation dep.junitApi
    testRuntimeOnly dep.junitEngine

    // Platform/BOM dependencies constrain versions only.
    api enforcedPlatform(dep.grpcBom)
    api enforcedPlatform(dep.jacksonBom)
    api enforcedPlatform(dep.jerseyBom)
    api enforcedPlatform(dep.jettyBom)
    api enforcedPlatform(dep.kotlinBom)
    api enforcedPlatform(dep.nettyBom)
  }

  // We have to set the dokka configuration after evaluation since the com.vanniktech.maven.publish
  // plugin overwrites our dokka configuration on projects where it's applied.
  afterEvaluate { p ->
    p.tasks.dokka {
      reportUndocumented = false
      skipDeprecated = true
      jdkVersion = 8
    }
  }

  test {
    useJUnitPlatform()
    testLogging {
      events "started", "passed", "skipped", "failed"
      exceptionFormat = 'full'
    }
  }
  if (rootProject.file("hooks.gradle").exists()) {
    apply from: rootProject.file("hooks.gradle")
  }

  if (["misk-aws","misk-events","misk-jobqueue","misk-hibernate","misk-hibernate-testing"].contains(name)) {
    rootProject.getTasksByName("testShardHibernate", false).first().dependsOn.add(test)
  } else {
    rootProject.getTasksByName("testShardNonHibernate", false).first().dependsOn.add(test)
  }

  if (!name.startsWith("samples:")) {
    apply plugin: 'com.vanniktech.maven.publish'
  }

  // Workaround the Gradle bug resolving multiplatform dependencies.
  // https://github.com/square/okio/issues/647
  configurations.all { configuration ->
    if (name.contains('kapt') || name.contains("wire") || name.contains("proto") || name.contains("Proto")) {
      attributes.attribute(Usage.USAGE_ATTRIBUTE, subProject.objects.named(Usage.class, Usage.JAVA_RUNTIME))
    }
  }
}
