buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "com.squareup.wire:wire-gradle-plugin:3.0.0-alpha01"
  }
}

plugins {
  id "org.jetbrains.kotlin.jvm" version "1.3.21" apply false
  id "com.vanniktech.maven.publish" version "0.8.0" apply false
  id 'com.gradle.build-scan' version "2.1"
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

apply from: file("./dependencies.gradle")

tasks.register("testShardNonHibernate") {
  group = "Continuous integration"
  description = "Runs all tests that don't depend on misk-hibernate. " +
          "This target is intended for manually sharding tests to make CI faster."
}

tasks.register("testShardHibernate") {
  group = "Continuous integration"
  description = "Runs all tests that depend on misk-hibernate. " +
          "This target is intended for manually sharding tests to make CI faster."
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'kotlin'

  buildscript {
    repositories {
      mavenCentral()
      jcenter()
    }

    dependencies {
      classpath dep.shadowJarPlugin
    }
  }
  repositories {
    mavenCentral()
    jcenter()
  }
  compileKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
      allWarningsAsErrors = true
    }
  }
  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
      allWarningsAsErrors = true
    }
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
  }

  dependencies {
    testImplementation dep.junitApi
    testRuntimeOnly dep.junitEngine
  }
  test {
    useJUnitPlatform()
    testLogging {
      events "started", "passed", "skipped", "failed"
      exceptionFormat = 'full'
    }
  }
  if (rootProject.file("hooks.gradle").exists()) {
    apply from: rootProject.file("hooks.gradle")
  }

  if (["misk-aws","misk-events","misk-jobqueue","misk-hibernate","misk-hibernate-testing"].contains(name)) {
    rootProject.getTasksByName("testShardHibernate", false).first().dependsOn.add(test)
  } else {
    rootProject.getTasksByName("testShardNonHibernate", false).first().dependsOn.add(test)
  }

  if (!name.startsWith("samples:")) {
    apply plugin: 'com.vanniktech.maven.publish'
  }
}
