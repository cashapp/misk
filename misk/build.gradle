import org.junit.platform.console.options.Details

apply plugin: 'kotlin'
kotlin {
  experimental {
    coroutines 'enable'
  }
}
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: "com.vanniktech.maven.publish"

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    allWarningsAsErrors = true
  }
}
compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
    allWarningsAsErrors = true
  }
}

sourceSets {
  main.java.srcDirs += 'src/main/kotlin/'
  test.java.srcDirs += 'src/test/kotlin/'
}

junitPlatform {
  details Details.VERBOSE
}

dependencies {
  compile dep.kotlinStdLib
  compile dep.kotlinTest
  compile dep.bouncycastle
  compile dep.guava
  compile dep.guice
  compile dep.guiceMultibindings
  compile dep.javaxInject
  compile dep.okHttp
  compile dep.okio
  compile dep.kotlinReflection
  compile dep.moshi
  compile dep.conscrypt
  compile dep.jettyAlpnConscypt
  compile dep.jettyHttp2
  compile dep.jettyServer
  compile dep.servletApi
  compile dep.jettyServlet
  compile dep.jettyWebsocketServlet
  compile dep.jettyWebsocketServer
  compile dep.kubernetesClient
  compile dep.wireRuntime
  compile dep.loggingApi
  compile dep.jacksonDatabind
  compile dep.jacksonDataformatYaml
  compile dep.jacksonKotlin
  compile dep.jacksonJsr310
  compile dep.jCommander
  compile dep.openTracing
  compile dep.openTracingUtil
  compile dep.openTracingOkHttp
  compile dep.retrofit
  compile dep.retrofitMoshi
  compile dep.retrofitWire
  compile dep.logbackClassic
  compile dep.slf4jApi
  compile dep.jaxbApi
  compile dep.prometheusClient
  compile dep.prometheusHotspot

  testCompile dep.kotlinxCoroutines
  testCompile dep.mockitoCore
  testCompile project(':misk-testing')
}

task webpackMiskCommon(type: Exec) {
  inputs.file("web/@misk/common/package-lock.json").
          withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/@misk/common/yarn.lock").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir("web/@misk/common/src").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/@misk/common/webpack.config.js").
          withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/@misk/common/webpack.config.static.js").
          withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/@misk/common/webpack.config.vendors.js").
          withPathSensitivity(PathSensitivity.RELATIVE)
  outputs.dir("web/@misk/common/lib")
  outputs.cacheIf { true }

  workingDir "web/@misk/common"
  commandLine "yarn", "gradle"
}


task webpackMiskComponents(type: Exec) {
  inputs.file("web/@misk/components/package-lock.json")
  inputs.file("web/@misk/components/yarn.lock").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir("web/@misk/components/src").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/@misk/components/webpack.config.js").
          withPathSensitivity(PathSensitivity.RELATIVE)
  outputs.dir("web/@misk/components/lib")
  outputs.cacheIf { true }

  workingDir "web/@misk/components"
  commandLine "yarn", "gradle"
}


task webpackTabsConfig(type: Exec) {
  inputs.file("web/tabs/config/package-lock.json").
          withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/tabs/config/yarn.lock").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir("web/tabs/config/src").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/tabs/config/webpack.config.js").
          withPathSensitivity(PathSensitivity.RELATIVE)
  outputs.dir("web/tabs/config/dist")
  outputs.cacheIf { true }

  workingDir "web/tabs/config"
  commandLine "yarn", "gradle"
}


task webpackTabsExample(type: Exec) {
  inputs.file("web/tabs/example/package-lock.json").
          withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/tabs/example/yarn.lock").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir("web/tabs/example/src").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/tabs/example/webpack.config.js").
          withPathSensitivity(PathSensitivity.RELATIVE)
  outputs.dir("web/tabs/example/dist")
  outputs.cacheIf { true }

  workingDir "web/tabs/example"
  commandLine "yarn", "gradle"
}


task webpackTabsLoader(type: Exec) {
  inputs.file("web/tabs/loader/package-lock.json").
          withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/tabs/loader/yarn.lock").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.dir("web/tabs/loader/src").withPathSensitivity(PathSensitivity.RELATIVE)
  inputs.file("web/tabs/loader/webpack.config.js").
          withPathSensitivity(PathSensitivity.RELATIVE)
  outputs.dir("web/tabs/loader/dist")
  outputs.cacheIf { true }

  workingDir "web/tabs/loader"
  commandLine "yarn", "gradle"
}

jar.dependsOn webpackMiskCommon
jar.dependsOn webpackMiskComponents
jar.dependsOn webpackTabsConfig
jar.dependsOn webpackTabsExample
jar.dependsOn webpackTabsLoader

jar {
  into("web/") {
    from("./web/@misk/common/lib/")
    from("./web/@misk/components/dist/")
    from("./web/@misk/dev/")
    from("./web/@misk/tslint/")
    from("./web/tabs/config/dist/")
    from("./web/tabs/example/dist/")
    from("./web/tabs/loader/dist/")
  }
}

if (rootProject.file("hooks.gradle").exists()) {
  apply from: rootProject.file("hooks.gradle")
}
