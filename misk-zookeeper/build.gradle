buildscript {
  dependencies {
    classpath dep.kotlinAllOpenPlugin
    classpath dep.kotlinNoArgPlugin
  }
}

apply plugin: 'kotlin-allopen'
apply plugin: 'kotlin-jpa'

allOpen {
  annotation("javax.persistence.Entity")
  annotation("javax.persistence.Embeddable")
  annotation("javax.persistence.MappedSuperclass")
}

dependencies {
  implementation dep.guice
  implementation dep.loggingApi
  implementation(dep.curatorFramework) {
    exclude(group: 'org.slf4j', module: 'slf4j-log4j12')
    exclude(group: 'log4j', module: 'log4j')
  }
  implementation(dep.zookeeper) {
    exclude(group: 'org.slf4j', module: 'slf4j-log4j12')
    exclude(group: 'log4j', module: 'log4j')
  }
  implementation project(':misk')
  implementation project(':misk-core')
  implementation project(':misk-inject')
  implementation project(':misk-service')

  testImplementation dep.docker
  testImplementation dep.junitApi
  testImplementation(dep.zookeeper) {
    exclude(group: 'org.slf4j', module: 'slf4j-log4j12')
    exclude(group: 'log4j', module: 'log4j')
  }
  testImplementation project(':misk-testing')
  testImplementation project(':misk-zookeeper-testing')
  testImplementation dep.assertj
  testImplementation dep.kotlinTest
  testImplementation dep.mockitoCore
}

sourceSets {
  test {
    resources {
      srcDir '../misk-zookeeper-testing/src/main/resources'
    }
  }
}

afterEvaluate { project ->
  project.tasks.dokka {
    outputDirectory = "$rootDir/docs/0.x"
    outputFormat = 'gfm'
  }
}

apply from: "$rootDir/gradle-mvn-publish.gradle"
